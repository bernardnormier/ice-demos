// **********************************************************************
//
// Copyright (c) 2003-2018 ZeroC, Inc. All rights reserved.
//
// **********************************************************************

def demoIceGridProject(name) {
    project(name) {
        slice {
            java {
                include = ["${projectDir}"]
                srcDir = '.'
            }
        }

        sourceSets {
            main {
                java {
                    srcDir '.'
                }
            }
        }

        dependencies {
            implementation "com.zeroc:ice:${project.iceArtifactVersion}"
            implementation "com.zeroc:icessl:${project.iceArtifactVersion}"
            implementation "com.zeroc:icegrid:${project.iceArtifactVersion}"
            implementation "com.zeroc:icelocatordiscovery:${project.iceArtifactVersion}"
            runtime "org.apache.commons:commons-compress:1.14"
        }

        task makeDbDirs {
            doLast {
                file('db/registry').mkdirs()
                file('db/node').mkdirs()
            }
        }
        compileJava.dependsOn(makeDbDirs)

        clean {
            delete 'db'
        }

        // Don't create the default jar.
        jar.enabled = false
    }
}

demoIceGridProject(":demoIceGridSimple")
demoIceGridProject(":demoIceGridIcebox")

project(":demoIceGridSimple") {
    demoJar("client")
    demoJar("server")
}

project(":demoIceGridIcebox") {
    dependencies {
        implementation "com.zeroc:icessl:${project.iceArtifactVersion}"
        implementation "com.zeroc:icebox:${project.iceArtifactVersion}"
    }
    demoJar("client")
    demoJar("service")
    demoIceBoxServer()
}
